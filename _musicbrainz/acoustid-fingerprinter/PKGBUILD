# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Wieland Hoffmann <the_mineo@web.de>
pkgname=acoustid-fingerprinter
pkgver=0.6
pkgrel=5
pkgdesc="Acoustid Fingerprinter is a cross-platform GUI application for submitting audio fingerprints generated by Chromaprint."
arch=('x86_64' 'i686')
url="http://acoustid.org/fingerprinter"
license=('GPL2')
depends=('ffmpeg' 'qt4' 'chromaprint' 'taglib')
makedepends=('cmake')
source=("https://bitbucket.org/acoustid/$pkgname/downloads/$pkgname-$pkgver.tar.gz" libav10.patch CodecID.patch)
sha256sums=('c0165d4210a181b2baa5e72159bf3f1a9b76398da8794305ddf8e1923ca76f32'
            '0212ac585ac5b923ef1c84d6638bfd76d8339e5af4e69b11fd09084d2d54868e'
            '8e8f3a136aa0da0e64f154c7724d8092e857177e1cf628e50921df3f615585d2')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < ../libav10.patch
  patch -p1 < ../CodecID.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DQT_QMAKE_EXECUTABLE=qmake-qt4 .
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
