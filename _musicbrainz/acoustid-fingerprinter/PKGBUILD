# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Wieland Hoffmann <the_mineo@web.de>
pkgname=acoustid-fingerprinter
pkgver=0.6
pkgrel=4
pkgdesc="Acoustid Fingerprinter is a cross-platform GUI application for submitting audio fingerprints generated by Chromaprint."
arch=('x86_64' 'i686')
url="http://acoustid.org/fingerprinter"
license=('GPL2')
depends=('ffmpeg-compat2' 'qt4' 'chromaprint' 'taglib')
makedepends=('cmake')
source=("https://bitbucket.org/acoustid/$pkgname/downloads/$pkgname-$pkgver.tar.gz" ffmpeg-compat.patch)
sha256sums=('c0165d4210a181b2baa5e72159bf3f1a9b76398da8794305ddf8e1923ca76f32'
            'e1a44fff26fc5d35b9f1648043310a6a77046f1a4e094d4c6876d70610fce6ff')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < ../ffmpeg-compat.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DQT_QMAKE_EXECUTABLE=qmake-qt4 .
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
