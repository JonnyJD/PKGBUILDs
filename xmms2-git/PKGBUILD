# Maintainer: Christopher Rosell <chrippa@tanuki.se>
# Contributor: Xilon <xilon@gmail.com>
# Contributor: lh <jarryson@gmail.com>
# Contributor: Aaron 'venisonslurpee' Laursen <venisonslurpee@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=xmms2-git
pkgver=20121101
pkgrel=1
pkgdesc="complete rewrite of the popular music player"
arch=('i686' 'x86_64')
url="http://xmms2.org/"
license=('GPL' 'LGPL')
backup=('etc/conf.d/xmms2d.conf')
depends=('sqlite3' 'glib2' 'readline')
makedepends=('alsa-lib' 'avahi' 'boost' 'curl' 'faad2'
	'ffmpeg' 'fftw' 'flac' 'glib2' 'jack' 'libao' 'libcdio'
	'libdiscid' 'libgme' 'libmms' 'libmodplug' 'libmpcdec'
	'libofa' 'libsamplerate' 'libshout' 'libvorbis'
	'libxml2' 'mpg123' 'perl' 'pulseaudio' 'cython' 'python'
	'ruby' 'sidplay2-libs' 'smbclient' 'speex' 'wavpack'
	'cunit')
optdepends=('alsa-lib: ALSA audio output'
	'avahi: announce xmms2d via bonjour/mDNS/zeroconf'
	'boost: C++ language bindings'
	'curl: play HTTP streams'
	'faad2: AAC support'
	'ffmpeg: WMA, avcodec & avformat support'
	'fftw: calculations for visualization'
	'flac: FLAC support'
	'jack: JACK audio output'
	'libao: liboa audio output'
	'libdiscid: cdda support'
	'libcdio: cdda support'
	'libofa: MusicDNS fingerprinting'
	'libgme: support various game music formats'
	'libmms: play MMS streams'
	'libmodplug: to play MOD files'
	'libmpcdec: Musepack support'
	'libpulse: PulseAudio audio output'
	'libsamplerate: vocoder support'
	'libshout: Icecast audio output'
	'libvorbis: Ogg Vorbis support'
	'libxml2: XSPF and podcast support'
	'mpg123: alternative MP3 plugin'
	'oss: OSS audio output'
	'perl: Perl language bindings'
	'ruby: Ruby language bindings'
	'sidplay2-libs: to play a Commodore 64 music files AKA zaks'
	'smbclient: direct CIFS/SMB access'
	'speex: Speex support'
	'wavpack: WavPack support')
conflicts=('xmms2' 'xmms2-devel')
provides=('xmms2')
install=xmms2.install
source=(xmms2d.conf xmms2d.rc)
md5sums=('af13c937bf3c86b77ae6820107aab9b8'
         '893371ba4dc7b428322e44329683211f')

_gitroot="git://git.xmms.se/xmms2/xmms2-devel"
_gitname="xmms2-devel"

build() {
  cd $srcdir
  msg "Connecting to git.xmms.se GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    git submodule update
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
    cd $_gitname
    git submodule init
    git submodule update
  fi

  cd $srcdir
  if [ -d $_gitname-build ]; then
    rm -rf $_gitname-build
  fi

  cp -a $_gitname $_gitname-build
  cd $_gitname-build

  ./waf configure --prefix=/usr \
	--with-optionals=python,launcher,xmmsclient++,xmmsclient++-glib,perl,ruby,nycli,mdns,pixmaps,medialib-updater
  ./waf build
}

package() {
  cd $srcdir/$_gitname-build
  ./waf --destdir=${pkgdir} install

  install -D -m 0755 ../xmms2d.rc ${pkgdir}/etc/rc.d/xmms2d
  install -D -m 0644 ../xmms2d.conf ${pkgdir}/etc/conf.d/xmms2d.conf
}

# vim:set ts=2 sw=2 et:
