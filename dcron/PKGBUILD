# Contributor:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Paul Mattal <paul.archlinux.org>
# Contributor: static DOT vortex AT gmx.com

pkgname=dcron
pkgver=4.5
pkgrel=5
pkgdesc="dillon's lightweight cron daemon"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.jimpryor.net/linux/dcron.html"
backup=('var/spool/cron/root' 'etc/conf.d/crond')
depends=('bash')
provides=('cron')
conflicts=('cron')
optdepends=('smtp-server: sending cron job output via email')
install=$pkgname.install
source=(http://www.jimpryor.net/linux/releases/${pkgname}-${pkgver}.tar.gz
        service)
md5sums=('078833f3281f96944fc30392b1888326'
         'e43c3d758384ac644f5e19f460ab5f46')

build() {
    cd "$srcdir/${pkgname}-${pkgver}"

    # fix paths to point to /usr/bin
    sed -i 's=/usr/sbin/sendmail=/usr/bin/sendmail=g' defs.h
    sed -i 's=/usr/sbin/run-cron=/usr/bin/run-cron=g' extra/root.crontab

    # by default, any member of group "users" can edit their own crontab
    make \
        PREFIX=/usr \
        SBINDIR=/usr/bin \
        CRONTAB_GROUP=users \
        CRONTABS=/var/spool/cron \
        CRONSTAMPS=/var/spool/cronstamps
}

package() {
    cd "$srcdir/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install

    # install standard configuration and scripts
    install -d -m755 "$pkgdir/etc/cron."{hourly,daily,weekly,monthly} 

    install -D -m755 extra/run-cron "$pkgdir/usr/bin/run-cron"
    install -D -m0600 extra/root.crontab "$pkgdir/var/spool/cron/root" 
    install -D -m755 extra/crond.rc "$pkgdir/etc/rc.d/crond"
    install -D -m0644 extra/crond.conf "$pkgdir/etc/conf.d/crond"
    install -D -m644 extra/crontab.vim "$pkgdir/usr/share/vim/vimfiles/ftplugin/crontab.vim"
    sed -i -e 's=/var/spool/cron/cronstamps=/var/spool/cronstamps=' extra/prune-cronstamps
    install -D -m755 extra/prune-cronstamps "$pkgdir/etc/cron.d/prune-cronstamps"
    install -Dm644  $srcdir/service $pkgdir/usr/lib/systemd/system/dcron.service
}