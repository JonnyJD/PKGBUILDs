# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Brice Maron <brice@bmaron.net>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Steven Allen <steven {at} stebalien {dot} com>

pkgname=python-keyring
pkgver=3.7
pkgrel=2
pkgdesc="Store and access your passwords safely."
arch=(any)
url="http://pypi.python.org/pypi/keyring"
license=('PSF')
depends=('python')
source=("http://pypi.python.org/packages/source/k/keyring/keyring-$pkgver.zip"
        kwallet.patch)
checkdepends=('python-nose' 'python-mock')
optdepends=('libgnome-keyring: Gnome integration'
            'python-gobject: Gnome integration'
            'kdebindings-python: Kde integration'
            'python-crypto: CLI keyring'
            'python-secretstorage: SecretService DBus API (GNOME/KDE)')
md5sums=('7746c545debacf983ec1c01966cacec3'
         '3ee835c05b40204f14bbfaec4ffc1d24')

prepare() {
  cd "$srcdir/keyring-$pkgver"
  patch -p1 < ../kwallet.patch
}

build() {
  cd "$srcdir/keyring-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/keyring-$pkgver"
  env - nosetests3
}

package() {
  cd "$srcdir/keyring-$pkgver"
  python setup.py install --root=$pkgdir --optimize=1
}

# vim:set ts=2 sw=2 et:
