# Contributor: Tau Tsao <realturner at gmail.com>
pkgname=xrdp-git
_gitname=xrdp
pkgver=1409.d3fb328
pkgrel=1
pkgdesc="An open source remote desktop protocol(rdp) server - GIT version"
url="https://github.com/FreeRDP/xrdp"
arch=('i686' 'x86_64')
license=('GPL')
groups=()
depends=('tigervnc')
makedepends=('git' 'autoconf' 'automake' 'libtool')
provides=()
conflicts=('xrdp')
replaces=()
backup=()
options=()
source=("git://github.com/FreeRDP/xrdp.git")
noextract=()
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$_gitname"
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.ac
  ./bootstrap
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make || return 1
}

package() {
  cd "$_gitname"
  make DESTDIR=${pkgdir} install || return 1

  mv ${pkgdir}/usr/sbin/* ${pkgdir}/usr/bin/
  rm -rf ${pkgdir}/usr/sbin

  cd ${pkgdir}/etc
  mkdir rc.d/
  ln -s ../xrdp/xrdp.sh rc.d/rdpd
  sed -i 's|/usr/local/|/usr/|'  xrdp/xrdp.sh
  cd ${pkgdir}
  mkdir -p usr/lib/systemd/system

  cp ${srcdir}/${_gitname}/instfiles/*.service usr/lib/systemd/system
  sed -i 's|/sysconfig/xrdp|/xrdp/xrdp.ini|' usr/lib/systemd/system/xrdp.service
  sed -i 's|/sysconfig/xrdp|/xrdp/xrdp.ini|' usr/lib/systemd/system/xrdp-sesman.service
}
