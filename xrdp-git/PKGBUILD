# Contributor: Tau Tsao <realturner at gmail.com>
pkgname=xrdp-git
_gitname=xrdp
pkgver=1415.2eb6fc4
pkgrel=1
pkgdesc="An open source remote desktop protocol(rdp) server - GIT version"
url="https://github.com/FreeRDP/xrdp"
arch=('i686' 'x86_64')
license=('Apache')
groups=()
depends=('tigervnc')
makedepends=('git' 'autoconf' 'automake' 'libtool')
provides=()
conflicts=('xrdp')
replaces=()
backup=('etc/xrdp/sesman.ini' 'etc/xrdp/xrdp.ini')
options=()
install=xrdp-git.install
source=("git://github.com/FreeRDP/xrdp.git")
noextract=()
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$_gitname"

  sed -i 's|/sysconfig/xrdp|/xrdp/xrdp.ini|' instfiles/xrdp.service
  sed -i 's|/sysconfig/xrdp|/xrdp/xrdp.ini|' instfiles/xrdp-sesman.service

  sed -i 's|/usr/local/sbin|/usr/bin|' instfiles/xrdp.sh
  sed -i 's|/usr/sbin|/usr/bin|' instfiles/xrdp.service
  sed -i 's|/usr/sbin|/usr/bin|' instfiles/xrdp-sesman.service
}

build() {
  cd "$_gitname"
  ./bootstrap
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$_gitname"
  make DESTDIR=${pkgdir} install

  mv ${pkgdir}/usr/sbin/* ${pkgdir}/usr/bin/
  rm -rf ${pkgdir}/usr/sbin

  cd ${pkgdir}/etc
  mkdir rc.d/
  ln -s ../xrdp/xrdp.sh rc.d/rdpd

  cd ${pkgdir}
  mkdir -p usr/lib/systemd/system
  cp ${srcdir}/${_gitname}/instfiles/*.service usr/lib/systemd/system
}
